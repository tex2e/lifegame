
IDENTIFICATION DIVISION.
PROGRAM-ID. MAIN.

DATA DIVISION.
  WORKING-STORAGE SECTION.
    01 HEIGHT PIC 99 VALUE 20.
    01 WIDTH PIC 99 VALUE 40.
    01 FIELD.
      03 ROWS OCCURS 20.
        05 CELLS OCCURS 40.
          07 CELL PIC 9.
    01 NEWFIELD.
      03 ROWS OCCURS 20.
        05 CELLS OCCURS 40.
          07 CELL PIC 9.
    01 X PIC 99.
    01 Y PIC 99.
    01 XI PIC 9.
    01 YI PIC 9.
    01 X-I PIC 99.
    01 Y-I PIC 99.
    01 RANDOM-VALUE PIC 9.
    01 CELL-COUNT PIC 9.

PROCEDURE DIVISION.
  MAIN SECTION.
    PERFORM INIT-FIELD-PARAGRAPH.
    PERFORM LOOP-PARAGRAPH 100 TIMES.
    STOP RUN.

  INIT-FIELD-PARAGRAPH.
    PERFORM VARYING Y FROM 1 BY 1 UNTIL Y > HEIGHT
      PERFORM VARYING X FROM 1 BY 1 UNTIL X > WIDTH
        COMPUTE RANDOM-VALUE = FUNCTION RANDOM() * 2
        MOVE RANDOM-VALUE TO CELL OF FIELD(Y X)
      END-PERFORM
    END-PERFORM.

  LOOP-PARAGRAPH.
    PERFORM CLEAR-SCREEN-PARAGRAPH.
    PERFORM DUMP-FIELD-PARAGRAPH.
    CALL "SYSTEM" USING "sleep 0.1".
    PERFORM EVOLVE-PARAGRAPH.
    MOVE NEWFIELD TO FIELD. *> prepare next

  EVOLVE-PARAGRAPH.
    PERFORM VARYING Y FROM 1 BY 1 UNTIL Y > HEIGHT
      PERFORM VARYING X FROM 1 BY 1 UNTIL X > WIDTH
        *> count alive neighbors
        MOVE 0 TO CELL-COUNT
        PERFORM VARYING YI FROM 0 BY 1 UNTIL YI > 2
          PERFORM VARYING XI FROM 0 BY 1 UNTIL XI > 2
            COMPUTE X-I = FUNCTION MOD(X + XI - 2 WIDTH) + 1
            COMPUTE Y-I = FUNCTION MOD(Y + YI - 2 HEIGHT) + 1
            ADD CELL OF FIELD(Y-I X-I) TO CELL-COUNT
          END-PERFORM
        END-PERFORM
        SUBTRACT CELL OF FIELD(Y X) FROM CELL-COUNT

        *> dead or alive
        EVALUATE CELL-COUNT
          WHEN 2
            MOVE CELL OF FIELD(Y X) TO CELL OF NEWFIELD(Y X)
          WHEN 3
            MOVE 1 TO CELL OF NEWFIELD(Y X)
          WHEN OTHER
            MOVE 0 TO CELL OF NEWFIELD(Y X)
      END-PERFORM
    END-PERFORM.

  DUMP-FIELD-PARAGRAPH.
    PERFORM VARYING Y FROM 1 BY 1 UNTIL Y > HEIGHT
      PERFORM VARYING X FROM 1 BY 1 UNTIL X > WIDTH
        EVALUATE CELL OF FIELD(Y X)
          WHEN 0
            DISPLAY " " WITH NO ADVANCING
          WHEN 1
            DISPLAY "o" WITH NO ADVANCING
      END-PERFORM
      DISPLAY " "
    END-PERFORM.

  CLEAR-SCREEN-PARAGRAPH.
    CALL "SYSTEM" USING "clear".

END PROGRAM MAIN.
